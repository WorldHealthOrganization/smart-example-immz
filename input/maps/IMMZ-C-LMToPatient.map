map "http://worldhealthorganization.github.io/smart-immunizations-mini/StructureMap/IMMZ-C-LMToPatient" = "IMMZ-C-LMToPatient"

uses "http://worldhealthorganization.github.io/smart-immunizations-mini/StructureDefinition/IMMZ-C-register-client" alias IMMZC as target
uses "http://hl7.org/fhir/StructureDefinition/Patient" alias Patient as target

group IMMZCToPatient (source immzc : IMMZC, target patient: Patient) {
  immzc.uniqueId as id -> patient.identifier as identifier then {
    id -> identifier.value = id "SetIdentifierValue";
  } "SetIdentifier";
  
  immzc -> patient.name as pname then NameToHumanName( immzc, pname ) "SetName";

  immzc.sex as sex -> patient.gender = "translate(sex, 'http://worldhealthorganization.github.io/smart-immunizations-mini/ConceptMap/IMMZ-C-SexToAdministrativeGender', 'code')" "SetGender";

  immzc.birthDate as birthDate -> patient.birthDate = birthDate "SetBirthDate";

  immzc.caregiver as caregiver -> patient.contact as contact, contact.name as hname 
    then CGNameToHumanName( immzc, hname ) "SetCaregiver";

  immzc.phone as phone -> patient.telecom as telecom then {
    phone as content -> telecom.value = content, telecom.system = 'phone' "SetPhoneValue";
  } "SetPhone";

  immzc.administrativeArea as area -> patient.address as address then {
    area.coding first as coding -> address then {
      coding.code as content -> address.text = content "SetAddressToCode";
      coding.display as content -> address.text = content "SetAddressToDisplay";
    } "SetAddressText";
  } "SetAdministrativeArea";


}

group NameToHumanName (
  source immzc,
  target hname
) {
  immzc.fullName as fullName -> hname.text = fullName "SetFullName";
  immzc.firstName as firstName -> hname.given = firstName "SetFirstName";
  immzc.familyName as familyName -> hname.family = familyName "SetFamilyName";
}



group CGNameToHumanName (
  source immzc,
  target hname
) {
  immzc.cgfullName as fullName -> hname.text = fullName "SetFullName";
  immzc.cgfirstName as firstName -> hname.given = firstName "SetFirstName";
  immzc.cgfamilyName as familyName -> hname.family = familyName "SetFamilyName";
}